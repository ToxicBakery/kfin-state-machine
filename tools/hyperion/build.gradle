apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'signing'

group 'com.ToxicBakery.kfinstatemachine.hyperion'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        // Core dependency breaks lint due to the configuration setting, not sure how to fix this yet
        tasks.lint.enabled = false
        warningsAsErrors true
        abortOnError true
    }

    resourcePrefix 'kfin_'
}

dependencies {
    testImplementation "junit:junit:4.12"

    implementation project(path: ':kfin', configuration: 'jvmRuntimeElements')
    api "com.willowtreeapps.hyperion:hyperion-core:$hyperion_verion"
    kapt "com.google.auto.service:auto-service:1.0-rc4"

    implementation "com.android.support:appcompat-v7:$supportLibrary_version"
    implementation "com.android.support:recyclerview-v7:$supportLibrary_version"
    implementation "com.android.support:design:$supportLibrary_version"
    implementation "com.android.support.constraint:constraint-layout:$contraint_layout_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "com.jakewharton.timber:timber:4.7.0"
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            pom.withXml {
                def root = asNode()
                root.children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    description POM_DESCRIPTION
                    name POM_NAME
                    url POM_URL
                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }
                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                            organization POM_DEVELOPER_ORGANIZATION
                            organizationUrl POM_DEVELOPER_ORGANIZATION_URL
                        }
                    }
                }
            }
        }
    }

    repositories {
        def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"

        maven {
            url !version.contains("SNAPSHOT") ? releaseUrl : snapshotUrl
            credentials {
                username System.getenv('SONATYPE_USERNAME') ?: ""
                password System.getenv('SONATYPE_PASSWORD') ?: ""
            }
        }
    }
}

signing {
    required false
    sign publishing.publications
}
